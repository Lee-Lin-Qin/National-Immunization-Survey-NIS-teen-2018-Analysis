/***************************************************************************************************/
/********************* THIS PROGRAM IS A GUIDE TO YOUR DATA MILESTONE REPORT PROJECT ***************/
/*************** IF YOU CHANGE ALL THE PATH AND REPLACE THEM WITH WHERE YOU STORED YOUR ************/
/****** NIS-TEEN PUF FILES, YOU CAN RUN THESE CODES TO ASSIST YOU WITH YOUR MILESTONE REPORTS ******/
/************* BEST WISHES. PLEASE NOTE I USED "VACCINE 1" AS MY VACCINE IN THESE EXAMPLES ***********/
/***************************************************************************************************/

/**************************************************************************/
/* Step 1:   ASSIGN SAS LIBRARIES AND FILE NAMES                          */
/**************************************************************************/

/***** REPLACE ALL THIS PATH " PATH-TO-YOUR NIS-TEEN 2018 DATA SET YOU CREATE " ******/
/***** ASLO REPLACE "VACCINE 1" WITH YOUR ASSIGNED VACCINE ******/

LIBNAME PUF "C:\Users\lqin1\Desktop\NISTEEN2018\";
OPTIONS FMTSEARCH = (PUF WORK LIBRARY);

%LET MYPATH = c:\Users\lqin1\Desktop\NISTEEN2018\RESULTS;

/**************************************************************************/
/* Step 2:   CREATE THE DATA SET FOR YOUR PROJECT                         */
/*           USING THE VARIABLE LIST GIVING FROM THE MAIN DATA SET        */
/**************************************************************************/

DATA MYPROJECT;
	SET PUF.NISTEENPUF18(KEEP = SEQNUMT PDAT2 PROVWT_C STRATUM YEAR AGE SEX RACEETHK EDUC1 AGEGRP_M_I 
						 MARITAL2 INCPORAR_I INS_STAT2_I CKUP_11_12 CEN_REG FACILITY STATE P_UTDHPV       
						 P_UTDHPV2 P_UTDHPV_15INT P_UTDMEN P_UTDMENACWY P_UTDTD P_UTDTDAP P_UTDTDAP7);


	/*** ASSIGNING NEW AGE OF TEEN VARIABLE:  1 = 13 YEARS, 2 = 14 YEARS, 3 = 15 YEARS, 4 = 16 YEARS, AND 5 = 17 YEARS ***/
	NEWAGE = .;
	IF AGE IN (13) THEN NEWAGE = 1;
	ELSE IF AGE IN (14) THEN NEWAGE = 2;
	ELSE IF AGE IN (15) THEN NEWAGE = 3;
	ELSE IF AGE IN (16) THEN NEWAGE = 4;
	ELSE IF AGE IN (17) THEN NEWAGE = 5;

/*	NEWAGE2 = AGE - 12;*/

	/*** ASSIGNING AGE GROUP OF TEEN VARIABLE:  1 = (13 - 15)YEARS; 2 = (16-17) YEARS. ***/
	TEENAGEGP = .;
	IF AGE IN (13, 14, 15) THEN TEENAGEGP = 1;
	ELSE IF AGE IN (16, 17) THEN TEENAGEGP = 2;

	/*** REGROUPING 11-12 YEAR OLD WELL-CHILD EXAM OR CHECK-UP OF TEEN: BY COMBINING DON'T KNOW /REFUSED / MISSING AS 3 ***/
	MYCKUP1112 = CKUP_11_12;
	IF  CKUP_11_12 IN (., 77, 99) THEN MYCKUP1112 = 3;

	/*** ASSIGNING MISSING FACILITY AS UNKNOW = 6 ***/
	MYFACILITY = FACILITY;
	IF  FACILITY IN (.) THEN MYFACILITY = 6;

	/*** INCOME TO POVERTY RATIO VARIABLE 1= <133%; 2=133% TO 322% ***/
	IF 0 <= INCPORAR_I < 1.33162 THEN MYINCTOPOV = 1;
	ELSE IF 1.33162 <= INCPORAR_I < 3.22046 THEN MYINCTOPOV = 2;

	/*** REASSIGNING RACE/ETHNICITY VARIABLES: 1 = NH-WHITES; 2 = NH-BLACKS; 3 = HISPANICS; 4 = NH-OTHERS ***/
	MYRACEETH = .;
	IF RACEETHK = 1 THEN MYRACEETH = 3;
	ELSE IF RACEETHK = 2 THEN MYRACEETH = 1;
	ELSE IF RACEETHK = 3 THEN MYRACEETH = 2;
	ELSE IF RACEETHK = 4 THEN MYRACEETH = 4;

RUN;

/***************************************************************************/
/*** THIS PROGRAM PRODUCES ALL YOUR VARIABLE CREATED IN THE SUBSET DATA  ***/
/***************************************************************************/

PROC CONTENTS DATA = MYPROJECT VARNUM;
RUN;

/*******************************************************************************************/
/*** THIS PROGRAM REMOVES ALL THE FORMATS FROM THE VARIABLES CREATED IN THE SUBSET DATA  ***/
/*******************************************************************************************/
DATA PROJECT;
	SET MYPROJECT;
	FORMAT _ALL_; /*** DELETE ALL THE FORMATS FROM THE ORIGINAL DATA SET ***/
RUN;

/*************************************************************************************************/
/*** THIS PROGRAM PRODUCES NEW FORMATS TO BE USED FOR MY VARIABLES CREATED IN THE SUBSET DATA  ***/
/*************************************************************************************************/

PROC FORMAT;
	VALUE MYAGE 1 = "13"
				2 = "14"
				3 = "15"
				4 = "16"
				5 = "17"
				;
	VALUE FMTAGRP 1 = "13 - 15"
				  2	= "16 - 17"
				  ;
	VALUE FMTSEX  1 = "MALE  "
				  2 = "FEMALE"
				  ;
	VALUE FMTRACE   1 = "NON-HISPANIC WHITE ONLY           "
					2 = "NON-HISPANIC BLACK ONLY           "
					3 = "HISPANIC                          "
					4 = "NON-HISPANIC OTHER + MULTIPLE RACE"
					;
	VALUE FMTCKUP 1 = "YES CHECK UP"
				  2 = "NO CHECK UP "
				  3 = "DK OR REF   "
				  ;
	VALUE FMTINS    1 = "PRIVATE INSURANCE"
					2 = "ANY MEDICAID     "
					3 = "OTHER INSURANCE  "
					4 = "UNINSURED        "
					;
	VALUE FMTMAGRP  1 = "<= 34 YEARS   "
					2 = "35 TO 44 YEARS"
					3 = ">= 45 YEARS   "
					; 	
	VALUE FMTMARRY  1 = "MARRIED                                                              "
					2 = "NEVER MARRIED/ WIDOWED/ DIVORCED/ SEPARATED/ DECEASED/ LIVING WITH PARTNER"
					;
	VALUE FMTMEDU   1 = "LESS THAN 12 YEARS                  "
					2 = "12 YEARS                            "
					3 = "MORE THAN 12 YEARS, NON-COLLEGE GRAD"
					4 = "COLLEGE GRADUATE                    "
					;
	VALUE FMTINCPR  1 = " 0% TO <133% "
					2 = " 133% TO 322%"
                    ;

	VALUE FMTFACTY  1 = "ALL PUBLIC FACILITIES                          "
					2 = "ALL HOSPITAL FACILITIES                        "
					3 = "ALL PRIVATE FACILITIES                         "
					4 = "ALL STD/SCHOOL/TEEN CLINICS OR OTHER FACILITIES"
					5 = "MIXED                                          "
					6 = "UNKNOWN/DK                                     "
					;
	VALUE FMTCENREG 1 = "NORTHEAST"
					2 = "MIDWEST  "
					3 = "SOUTH    "
					4 = "WEST     "
					;
	VALUE FMTUTD    0 = "NOT UTD"
					1 = "UTD    "
					;
	
	VALUE FMSTATE	. = "MISSING              "
					1 = "ALABAMA              "
					2 = "ALASKA               "
					3 = "                     "
					4 = "ARIZONA              "
					5 = "ARKANSAS             "
					6 = "CALIFORNIA           "
					7 = "                     "
					8 = "COLORADO             "
					9 = "CONNECTICUT          "
					10 = "DELAWARE            "
					11 = "DISTRICT OF COLUMBIA"
					12 = "FLORIDA             "
					13 = "GEORGIA             "
					14 = "                    "
					15 = "HAWAII              "
					16 = "IDAHO               "
					17 = "ILLINOIS            "
					18 = "INDIANA             "
					19 = "IOWA                "
					20 = "KANSAS              "
					21 = "KENTUCKY            "
					22 = "LOUISIANA           "
					23 = "MAINE               "
					24 = "MARYLAND            "
					25 = "MASSACHUSETTS       "
					26 = "MICHIGAN            "
					27 = "MINNESOTA           "
					28 = "MISSISSIPPI         "
					29 = "MISSOURI            "
					30 = "MONTANA             "
					31 = "NEBRASKA            "
					32 = "NEVADA              "
					33 = "NEW HAMPSHIRE       "
					34 = "NEW JERSEY          "
					35 = "NEW MEXICO          "
					36 = "NEW YORK            "
					37 = "NORTH CAROLINA      "
					38 = "NORTH DAKOTA        "
					39 = "OHIO                "
					40 = "OKLAHOMA            "
					41 = "OREGON              "
					42 = "PENNSYLVANIA        "
					43 = "                    "
					44 = "RHODE ISLAND        "
					45 = "SOUTH CAROLINA      "
					46 = "SOUTH DAKOTA        "
					47 = "TENNESSEE           "
					48 = "TEXAS               "
					49 = "UTAH                "
					50 = "VERMONT             "
					51 = "VIRGINIA            "
					52 = "                    "
					53 = "WASHINGTON          "
					54 = "WEST VIRGINIA       "
					55 = "WISCONSIN           "
					56 = "WYOMING             "
					;
RUN;

/********************************************************************************************/
/*** THIS PROGRAM PRODUCES THE DISTRIBUTION OF SAMPLE SIZES FOR EACH VARIABLE OF INTEREST ***/
/********************************************************************************************/

ODS OUTPUT ONEWAYFREQS = RESPONSETABLE; /*** THIS CREATES YOUR SAS DATA SET RESULTS FROM THE PROC FREQ RESULTS ***/
PROC FREQ DATA = PROJECT;
	WHERE PDAT2 = 1  AND PROVWT_C NE .; /*** USING ONLY ADEQUATE PROVIDER DATA ***/
	TABLES NEWAGE TEENAGEGP SEX MYRACEETH EDUC1 MARITAL2 AGEGRP_M_I MYINCTOPOV INS_STAT2_I MYCKUP1112 CEN_REG MYFACILITY STATE/ NOCUM NOPERCENT;
	TITLE1 '2018 NIS-TEEN PUBLIC-USE FILE';
	TITLE2 'UNWEIGHTED FREQUENCIES OF SELECTED CHARACTERISTICS OF ADOLESCENTS AGED 13-17 YEARS';

	FORMAT NEWAGE MYAGE.;
	FORMAT TEENAGEGP FMTAGRP.;
	FORMAT SEX FMTSEX.;
	FORMAT MYRACEETH FMTRACE.;
	FORMAT EDUC1 FMTMEDU.;
	FORMAT MARITAL2 FMTMARRY.;
	FORMAT AGEGRP_M_I FMTMAGRP.;
	FORMAT MYINCTOPOV FMTINCPR.;
	FORMAT INS_STAT2_I FMTINS.;
	FORMAT MYCKUP1112 FMTCKUP.;
	FORMAT CEN_REG FMTCENREG.;	
	FORMAT MYFACILITY FMTFACTY.;
	FORMAT STATE FMSTATE.;
RUN;

/*** THIS PROCEDURE DISPLAYS ALL THE VARIABLE NAMES IN THE DATA SET IN ALPHABETICAL ORDER ***/
PROC CONTENTS DATA = RESPONSETABLE SHORT;
RUN;

/*** THIS CREATES YOUR SAS DATA SET RESULTS FOR YOUR SAMPLE SIZES ***/
DATA RESPONSETABLEV1; 
	RETAIN TABLE MYVARIABLE MYVARCOUNT MYVARNUM FREQUENCY;
	LENGTH MYVARIABLE $40.; /*** CREATING THE LENGTH FOR THE CHARACTER VALUES TO BE PLACED IN THE VARIABLE MYVARIABLE ***/

	SET RESPONSETABLE(KEEP = AGEGRP_M_I CEN_REG EDUC1 FREQUENCY INS_STAT2_I MARITAL2 MYCKUP1112 MYFACILITY MYINCTOPOV 
					         MYRACEETH NEWAGE SEX STATE TEENAGEGP TABLE);

	/*** MYVARIABLE = THE LABEL FOR EACH VALUE OF VARIABLE; MYVARCOUNT = THE VARIABLE NUMBER IN THE TABLE; MYVARNUM = THE VALUE OF EACH VARIABLE ***/
	IF NEWAGE NE . THEN DO;
		MYVARIABLE = PUT(NEWAGE, MYAGE.);
		MYVARCOUNT = 1;
		MYVARNUM = NEWAGE;
	END;
	ELSE IF TEENAGEGP NE . THEN DO;
		MYVARIABLE = PUT(TEENAGEGP, FMTAGRP.);
		MYVARCOUNT = 2;
		MYVARNUM = TEENAGEGP;
	END;
	ELSE IF SEX NE . THEN DO;
		MYVARIABLE = PUT(SEX, FMTSEX.);
		MYVARCOUNT = 3;
		MYVARNUM = SEX; 
	END;
	ELSE IF MYRACEETH NE . THEN DO;
		MYVARIABLE = PUT(MYRACEETH, FMTRACE.);
		MYVARCOUNT = 4;
		MYVARNUM = MYRACEETH;
	END;
	ELSE IF EDUC1 NE . THEN DO;
		MYVARIABLE = PUT(EDUC1, FMTMEDU.);
		MYVARCOUNT = 5;
		MYVARNUM = EDUC1;
	END;
	ELSE IF MARITAL2 NE . THEN DO;
		MYVARIABLE = PUT(MARITAL2, FMTMARRY.);
		MYVARCOUNT = 6;
		MYVARNUM = MARITAL2;
	END;
	ELSE IF AGEGRP_M_I NE . THEN DO;
		MYVARIABLE = PUT(AGEGRP_M_I, FMTMAGRP.);
		MYVARCOUNT = 7;
		MYVARNUM = AGEGRP_M_I;
	END;
	ELSE IF MYINCTOPOV NE . THEN DO;
		MYVARIABLE = PUT(MYINCTOPOV, FMTINCPR.);
		MYVARCOUNT = 8;
		MYVARNUM = MYINCTOPOV;
	END;
	ELSE IF INS_STAT2_I NE . THEN DO;
		MYVARIABLE = PUT(INS_STAT2_I, FMTINS.);
		MYVARCOUNT = 9;
		MYVARNUM = INS_STAT2_I;
	END;
	ELSE IF MYCKUP1112 NE . THEN DO;
		MYVARIABLE = PUT(MYCKUP1112, FMTCKUP.);
		MYVARCOUNT = 10;
		MYVARNUM = MYCKUP1112;
	END;
	ELSE IF CEN_REG NE . THEN DO;
		MYVARIABLE = PUT(CEN_REG, FMTCENREG.);
		MYVARCOUNT = 11;
		MYVARNUM = CEN_REG;
	END;
	ELSE IF MYFACILITY NE . THEN DO;
		MYVARIABLE = PUT(MYFACILITY, FMTFACTY.);
		MYVARCOUNT = 12;
		MYVARNUM = MYFACILITY;
	END;
	ELSE IF STATE NE . THEN DO;
		MYVARIABLE = PUT(STATE, FMSTATE.);
		MYVARCOUNT = 13;
		MYVARNUM = STATE;
	END;

	KEEP TABLE MYVARIABLE MYVARCOUNT MYVARNUM FREQUENCY;
RUN;

/*** THIS PROCEDURE EXPORTS YOUR SAMPLE SIZES IN A MS EXCEL FILE ***/
PROC EXPORT DATA = RESPONSETABLEV1 OUTFILE ="&MYPATH.\TABLE1\TABLE1SAMPLESIZESNEW.xlsx" DBMS=XLSX REPLACE;
RUN;


/********************************************************************************************************************/
/*** THIS PROGRAM PRODUCES THE DISTRIBUTION OF WEIGHTED FREQUENCIES AND PERCENTAGES FOR EACH VARIABLE OF INTEREST ***/
/********************************************************************************************************************/

ODS OUTPUT ONEWAYFREQS = WTRESPONSETABLE;
PROC FREQ DATA = PROJECT;
	WHERE PDAT2 = 1; /*** USING ONLY ADEQUATE PROVIDER DATA ***/
	WEIGHT PROVWT_C; /*** TO INCLUDE WEIGHTS FOR WEIGHTED RESULTS ***/
	TABLES NEWAGE TEENAGEGP SEX MYRACEETH EDUC1 MARITAL2 AGEGRP_M_I MYINCTOPOV INS_STAT2_I MYCKUP1112 CEN_REG MYFACILITY STATE/ NOCUM;
	TITLE1 '2018 NIS-TEEN PUBLIC-USE FILE';
	TITLE2 'CHI-SQUARE TEST RESULTS FOR WEIGHTED VACCINATION COVERAGES BY SELECTED VARIABLES';

	FORMAT NEWAGE MYAGE.;
	FORMAT TEENAGEGP FMTAGRP.;
	FORMAT SEX FMTSEX.;
	FORMAT MYRACEETH FMTRACE.;
	FORMAT EDUC1 FMTMEDU.;
	FORMAT MARITAL2 FMTMARRY.;
	FORMAT AGEGRP_M_I FMTMAGRP.;
	FORMAT MYINCTOPOV FMTINCPR.;
	FORMAT INS_STAT2_I FMTINS.;
	FORMAT MYCKUP1112 FMTCKUP.;
	FORMAT CEN_REG FMTCENREG.;	
	FORMAT MYFACILITY FMTFACTY.;
	FORMAT STATE FMSTATE.;
RUN;

/*** THIS PROCEDURE DISPLAYS ALL THE VARIABLE NAMES IN THE DATA SET IN ALPHABETICAL ORDER ***/
PROC CONTENTS DATA = WTRESPONSETABLE SHORT;
RUN;

/*** THIS CREATES YOUR SAS DATA SET RESULTS FOR YOUR SAMPLE SIZES ***/
DATA WTRESPONSETABLEV1; 
	RETAIN TABLE MYVARIABLE MYVARCOUNT MYVARNUM PERCENT MYPERCENT;
	LENGTH MYVARIABLE $40.; /*** CREATING THE LENGTH FOR THE CHARACTER VALUES TO BE PLACED IN THE VARIABLE MYVARIABLE ***/

	SET WTRESPONSETABLE(KEEP =  TABLE PERCENT NEWAGE TEENAGEGP SEX MYRACEETH EDUC1 MARITAL2 AGEGRP_M_I MYINCTOPOV INS_STAT2_I MYCKUP1112 CEN_REG
						MYFACILITY STATE);

	MYPERCENT = PUT(PERCENT, 8.1); /*** FORMATING PERCENTAGES TO 1 DECIMAL PLACE ***/

	/*** MYVARIABLE = THE LABEL FOR EACH VALUE OF VARIABLE; MYVARCOUNT = THE VARIABLE NUMBER IN THE TABLE; MYVARNUM = THE VALUE OF EACH VARIABLE ***/
	IF NEWAGE NE . THEN DO;
		MYVARIABLE = PUT(NEWAGE, MYAGE.);
		MYVARCOUNT = 1;
		MYVARNUM = NEWAGE;
	END;
	ELSE IF TEENAGEGP NE . THEN DO;
		MYVARIABLE = PUT(TEENAGEGP, FMTAGRP.);
		MYVARCOUNT = 2;
		MYVARNUM = TEENAGEGP;
	END;
	ELSE IF SEX NE . THEN DO;
		MYVARIABLE = PUT(SEX, FMTSEX.);
		MYVARCOUNT = 3;
		MYVARNUM = SEX; 
	END;
	ELSE IF MYRACEETH NE . THEN DO;
		MYVARIABLE = PUT(MYRACEETH, FMTRACE.);
		MYVARCOUNT = 4;
		MYVARNUM = MYRACEETH;
	END;
	ELSE IF EDUC1 NE . THEN DO;
		MYVARIABLE = PUT(EDUC1, FMTMEDU.);
		MYVARCOUNT = 5;
		MYVARNUM = EDUC1;
	END;
	ELSE IF MARITAL2 NE . THEN DO;
		MYVARIABLE = PUT(MARITAL2, FMTMARRY.);
		MYVARCOUNT = 6;
		MYVARNUM = MARITAL2;
	END;
	ELSE IF AGEGRP_M_I NE . THEN DO;
		MYVARIABLE = PUT(AGEGRP_M_I, FMTMAGRP.);
		MYVARCOUNT = 7;
		MYVARNUM = AGEGRP_M_I;
	END;
	ELSE IF MYINCTOPOV NE . THEN DO;
		MYVARIABLE = PUT(MYINCTOPOV, FMTINCPR.);
		MYVARCOUNT = 8;
		MYVARNUM = MYINCTOPOV;
	END;
	ELSE IF INS_STAT2_I NE . THEN DO;
		MYVARIABLE = PUT(INS_STAT2_I, FMTINS.);
		MYVARCOUNT = 9;
		MYVARNUM = INS_STAT2_I;
	END;
	ELSE IF MYCKUP1112 NE . THEN DO;
		MYVARIABLE = PUT(MYCKUP1112, FMTCKUP.);
		MYVARCOUNT = 10;
		MYVARNUM = MYCKUP1112;
	END;
	ELSE IF CEN_REG NE . THEN DO;
		MYVARIABLE = PUT(CEN_REG, FMTCENREG.);
		MYVARCOUNT = 11;
		MYVARNUM = CEN_REG;
	END;
	ELSE IF MYFACILITY NE . THEN DO;
		MYVARIABLE = PUT(MYFACILITY, FMTFACTY.);
		MYVARCOUNT = 12;
		MYVARNUM = MYFACILITY;
	END;
	ELSE IF STATE NE . THEN DO;
		MYVARIABLE = PUT(STATE, FMSTATE.);
		MYVARCOUNT = 13;
		MYVARNUM = STATE;
	END;
	
	KEEP TABLE MYVARIABLE MYVARCOUNT MYVARNUM PERCENT MYPERCENT;
RUN;

/*** THIS PROCEDURE EXPORTS YOUR SAMPLE SIZES IN A MS EXCEL FILE ***/
PROC EXPORT DATA = WTRESPONSETABLEV1 OUTFILE ="&MYPATH.\TABLE1\TABLE1WEIGHTEDPERCENT.xlsx" DBMS=XLSX REPLACE;
RUN;

/****************************************************************************************************/
/*** THIS PROGRAM PRODUCES THE DISTRIBUTION OF WEIGHTED FREQUENCIES FOR EACH VARIABLE OF INTEREST ***/
/***      AND THE CHI-SQUARE STATISTICS AND P-VALUE FOR EACH VARIABLE OF INTEREST.                ***/
/****************************************************************************************************/

ODS OUTPUT CHISQ = MYCHISQTEST;
PROC FREQ DATA = PROJECT;
	WHERE PDAT2 = 1; /*** USING ONLY ADEQUATE PROVIDER DATA ***/
	WEIGHT PROVWT_C; /*** TO INCLUDE WEIGHTS FOR WEIGHTED RESULTS ***/
	TABLES (NEWAGE TEENAGEGP SEX MYRACEETH EDUC1 MARITAL2 AGEGRP_M_I MYINCTOPOV INS_STAT2_I MYCKUP1112 CEN_REG MYFACILITY)*P_UTDHPV / CHISQ;
	TITLE1 '2018 NIS-TEEN PUBLIC-USE FILE';
	TITLE2 'CHI-SQUARE TEST RESULTS FOR WEIGHTED VACCINATION COVERAGES BY SELECTED VARIABLES';

	FORMAT NEWAGE MYAGE.;
	FORMAT TEENAGEGP FMTAGRP.;
	FORMAT SEX FMTSEX.;
	FORMAT MYRACEETH FMTRACE.;
	FORMAT EDUC1 FMTMEDU.;
	FORMAT MARITAL2 FMTMARRY.;
	FORMAT AGEGRP_M_I FMTMAGRP.;
	FORMAT MYINCTOPOV FMTINCPR.;
	FORMAT INS_STAT2_I FMTINS.;
	FORMAT MYCKUP1112 FMTCKUP.;
	FORMAT CEN_REG FMTCENREG.;	
	FORMAT MYFACILITY FMTFACTY.;
	FORMAT P_UTDHPV FMTUTD.;
RUN;

/*** THIS PROCEDURE DISPLAYS ALL THE VARIABLE NAMES IN THE DATA SET IN ALPHABETICAL ORDER ***/
PROC CONTENTS DATA = MYCHISQTEST SHORT;
RUN;

DATA MYCHISQTESTV1;
	RETAIN TABLE STATISTIC MYVALUE MYPROB DF VALUE PROB;
	LENGTH MYVALUE MYPROB $15.;
	SET MYCHISQTEST;

	WHERE STATISTIC IN ("Chi-Square"); /*** SELECTING ONLY CHI-SQUARE RESULTS ***/

	MYVALUE = PUT(VALUE, 10.3); /*** FORMATTING THE CHI-SQUARE STATISTIC TO 3 DECIMAL PLACES. ***/
	MYPROB = PUT(PROB, 8.2); /*** FORMATTING THE CHI-SQUARE P-VALUES TO 2 DECIMAL PLACES. ***/
RUN; 

/*** THIS PROCEDURE EXPORTS YOUR VACCINATION COVERAGE ESTIMATES RESULTS IN A MS EXCEL FILE ***/
PROC EXPORT DATA = MYCHISQTESTV1 OUTFILE ="&MYPATH.\TABLE2\TABLE2CHISQUARE.xlsx" DBMS=XLSX REPLACE;
RUN;


/*****************************************************************************************/
/*** THIS PROGRAM PRODUCES A HISTOGRAM FOR AGE IN YEARS AT INTERVIEW OF THE ADOLESCENT ***/
/*****************************************************************************************/

/*** NOTE: YOUR CREATE HTML SHOULD BE CHECKED IN THE RESULTS WINDOW BEFORE YOU CAN SEE YOUR PLOTS ***/

PROC SGPLOT DATA=PROJECT;
	WHERE PDAT2 = 1; /*** USING ONLY ADEQUATE PROVIDER DATA ***/
    TITLE "Plotting Weighted Histogram Using Age (in Years) at Interview in NIS-Teen 2018 PUF";
    HISTOGRAM NEWAGE / BINSTART = 1 BINWIDTH = 1 NBINS = 5 WEIGHT = PROVWT_C;
    DENSITY NEWAGE;
RUN;


/************* REPLACE THE VARIABLE "NEWAGE" IN THE ABOVE PROGRAM FOR THE OTHER CHARACTERISTICS REQUIRED ***************/

/*******************************************************************************/
/*** THIS PROGRAM PRODUCES A VERTICAL BAR GRAPH FOR THE SEX OF THE ADOLECENT ***/
/*******************************************************************************/

PROC SGPLOT DATA=PROJECT;
	WHERE PDAT2 = 1; /*** USING ONLY ADEQUATE PROVIDER DATA ***/
    TITLE "Weighted Vertical Bar Chart Using Sex of the Adolescent in NIS-Teen 2018 PUF";
    VBAR SEX / WEIGHT = PROVWT_C STAT = PERCENT;
    FORMAT SEX FMTSEX.;
RUN;


/*********************************************************************************/
/*** THIS PROGRAM PRODUCES A HORIZONTAL BAR GRAPH FOR THE SEX OF THE ADOLECENT ***/
/*********************************************************************************/

PROC SGPLOT DATA=PROJECT;
	WHERE PDAT2 = 1; /*** USING ONLY ADEQUATE PROVIDER DATA ***/
    TITLE "Weighted Vertical Bar Chart Using Sex of the Adolescent in NIS-Teen 2018 PUF";
    HBAR SEX / WEIGHT = PROVWT_C STAT = PERCENT;
    FORMAT SEX FMTSEX.;
RUN;

/************* REPLACE THE VARIABLE "SEX" IN THE ABOVE PROGRAM FOR THE OTHER CHARACTERISTICS REQUIRED ***************/

/********************************************************************************/
/*** THIS PROGRAM PRODUCES A PIE CHART OF THE CENSUS REGION OF THE ADOLESCENT ***/
/********************************************************************************/

PROC GCHART DATA=PROJECT;
   TITLE "Unweighted Pie-Chart";
   TITLE "Using the Census Region of the Adolescent in NIS-Teen 2018 PUF";
   WHERE PDAT2 = 1  AND PROVWT_C NE .; /*** USING ONLY ADEQUATE PROVIDER DATA ***/
   PIE CEN_REG / SUMVAR = PROVWT_C TYPE=SUM PERCENT = ARROW NOHEADING LEGEND DISCRETE;
   FORMAT CEN_REG FMTCENREG.;
RUN;
QUIT;

/************* REPLACE THE VARIABLE "CEN_REG" THE ABOVE PROGRAM FOR THE OTHER CHARACTERISTICS REQUIRED ***************/


/*************************************************************************************************/
/*****    THIS PROGRAM PRODUCES THE DISTRIBUTION OF SAMPLE SIZES AND VACCINATION COVERAGE    *****/
/***** ESTIMATES WITH THEIR CORRESPONDING CONFIDENCE INTERVALS FOR EACH VARIABLE OF INTEREST *****/
/*************************************************************************************************/


/************* REPEAT THE PROGRAMS BELOW FOR ALL THE REMAINING CHARACTERISTICS REQUIRED ***************/
/*************************************      BEFORE YOU RUN THIS    ************************************/

/*******************************************************************************************************/
/* BELOW IS A SAMPLE CODE TO USED                                                                      */
/* ODS OUTPUT SUMMARY = ;                                                                              */
/* PROC MEANS DATA=PROJECT MEAN CLM MAXDEC = 3;                                                        */
/*      WHERE PDAT2 = 1  AND PROVWT_C NE .;                                                            */
/*  	TITLE "  ";                                                                                    */
/*  	CLASS ;                                                                                        */
/*  	WEIGHT PROVWT_C;                                                                               */
/*  	FREQ ;                                                                                         */
/*  	VAR ;                                                                                          */
/*  	FORMAT ;                                                                                       */
/*  	FORMAT ;                                                                                       */
/* RUN;                                                                                                */
/*******************************************************************************************************/

%MACRO MYESTIMATE(VAR1, VAR2, VAR3, VAR4, DESCRIPTION);
/*** &VAR1. VARIABLE ***/
ODS OUTPUT SUMMARY = MYMEANS&VAR1.; /*** GIVE A NAME TO THE SAS DATA SET TO BE CREATED ***/
PROC MEANS DATA=PROJECT MEAN CLM MAXDEC = 3;
	TITLE "2018 NIS-TEEN PUBLIC-USE FILE VACCINATION COVERAGE ESTIMATES BY DESCRIPTION";
	WHERE PDAT2 = 1  AND PROVWT_C NE .;
	CLASS &VAR1.; /*** INSERT YOUR SELECTED CHARACTERISTICS VARIABLE ***/
	WEIGHT PROVWT_C; /*** INSERT YOUR WEIGHT VARIABLE ***/
	FREQ &VAR1.; /*** INSERT YOUR SELECTED CHARACTERISTICS VARIABLE ***/
	VAR &VAR2.; /*** INSERT YOUR VACCINE VARIABLE ***/
	FORMAT &VAR1. &VAR3.;
	FORMAT &VAR2. FMTUTD.;
RUN;

/*** THIS PROCEDURE DISPLAYS ALL THE VARIABLE NAMES IN THE DATA SET IN ALPHABETICAL ORDER ***/
PROC CONTENTS DATA = MYMEANS&VAR1. SHORT;
RUN;

DATA MYMEANS&VAR1.V1;
	RETAIN MYCHARACT MYVARIABLE MYVARNUM MYRESULTS FINALESTIMATE &VAR2._MEAN &VAR2._LCLM &VAR2._UCLM;
	LENGTH MYRESULTS FINALESTIMATE $20. MYVARIABLE $40.;
	SET MYMEANS&VAR1.;

	MYESTMATE = PUT(ROUND(&VAR2._MEAN*100,0.1), 4.1); /*** ROUNDING THE POINT ESTIMATE AS A PERCENTAGE TO 1 DECIMAL PLACE ***/
	CONINTEL = '('||PUT(ROUND(&VAR2._LCLM*100,0.1), 4.1)||' - '||PUT(ROUND(&VAR2._UCLM*100,0.1), 4.1)||')'; /*** COMBINING THE LOWER AND UPPER CONFIDENCE LIMITS ***/
	FINALESTIMATE = CATX(" ",MYESTMATE," ",CONINTEL); /*** COMBINING THE POINT ESTIMATE AND CONFIDENCE LIMITS TO SHOW ONE RESULT ***/
	MYCIDIFF = (&VAR2._UCLM*100) - (&VAR2._LCLM*100); /*** FINDING THE DIFFERENCE IN CONFIDENCE WIDTH ***/
	IF (MYCIDIFF > 20) THEN MYRESULTS = FINALESTIMATE||"**"; /*** TESTING TO SEE IF THE DIFFERENCE IN CONFIDENCE WIDTH IS GREATER THAN 20 PERCENTAGE POINTS ***/
	ELSE MYRESULTS = FINALESTIMATE;

	/*** ASSIGNING MYVARIABLE = THE LABEL FOR EACH VALUE OF VARIABLE AND MYVARNUM = THE VALUE OF EACH VARIABLE ***/
	MYVARIABLE = PUT(&VAR1.,&VAR3.);
	MYVARNUM = &VAR1.;
	MYCHARACT = &VAR4.;

	KEEP MYCHARACT MYVARIABLE MYVARNUM MYRESULTS FINALESTIMATE &VAR2._MEAN &VAR2._LCLM &VAR2._UCLM;
RUN; 

/*** THIS PROCEDURE EXPORTS YOUR VACCINATION COVERAGE ESTIMATES RESULTS IN A MS EXCEL FILE ***/
/*** MAKE SURE YOU CHANGE FILE NAMES FOR EACH DIFFERENT VARIABLE (CHARACTERISTICS) ***/
PROC EXPORT DATA = MYMEANS&VAR1.V1 OUTFILE ="&MYPATH.\TABLE2\TABLE2VAX&VAR1..xlsx" DBMS=XLSX REPLACE;
RUN;

PROC APPEND BASE = ALLESTIMATES DATA = MYMEANS&VAR1.V1 FORCE;
RUN;

%MEND MYESTIMATE;

%MYESTIMATE(NEWAGE, P_UTDHPV, MYAGE., 1, "Age (years) at Interview");
%MYESTIMATE(TEENAGEGP, P_UTDHPV, FMTAGRP., 2, "Age Group (in years) at Interview");
%MYESTIMATE(SEX, P_UTDHPV, FMTSEX., 3, "Sex of Adolescent at Interview");
%MYESTIMATE(MYRACEETH, P_UTDHPV, FMTRACE., 4, "Sex of Adolescent at Interview");
%MYESTIMATE(EDUC1, P_UTDHPV, FMTMEDU., 5, "Sex of Adolescent at Interview");
%MYESTIMATE(MARITAL2, P_UTDHPV, FMTMARRY., 6, "Sex of Adolescent at Interview");
%MYESTIMATE(AGEGRP_M_I, P_UTDHPV, FMTMAGRP., 7, "Sex of Adolescent at Interview");
%MYESTIMATE(MYINCTOPOV, P_UTDHPV, FMTINCPR., 8, "Sex of Adolescent at Interview");
%MYESTIMATE(INS_STAT2_I, P_UTDHPV, FMTINS., 9, "Sex of Adolescent at Interview");
%MYESTIMATE(MYCKUP1112, P_UTDHPV, FMTCKUP., 10, "Sex of Adolescent at Interview");
%MYESTIMATE(CEN_REG, P_UTDHPV, FMTCENREG., 11, "Sex of Adolescent at Interview");
%MYESTIMATE(MYFACILITY, P_UTDHPV, FMTFACTY., 12, "Sex of Adolescent at Interview");
%MYESTIMATE(STATE, P_UTDHPV, FMSTATE., 13, "Sex of Adolescent at Interview");

DATA PUF.ALLESTIMATES;
	SET ALLESTIMATES;
RUN;

PROC EXPORT DATA = ALLESTIMATES OUTFILE ="&MYPATH.\TABLE2\TABLE2.xlsx" DBMS=XLSX REPLACE;
RUN;




/********************************************************************************************/
/*** THIS PROGRAM PRODUCES THE DISTRIBUTION OF SAMPLE SIZES FOR EACH VARIABLE OF INTEREST ***/
/***                            USING SAS SURVEY PROCEDURES                               ***/
/********************************************************************************************/

/* DEMONSTRATE THE SURVEYFREQ PROCEDURE */
TITLE1 "FREQUENCY ANALYSIS - PUF FOR NIS-TEEN 2018";
TITLE2 'UNIVARIATE FREQUENCIES ON VARIOUS VARIABLES';

ODS OUTPUT CROSSTABS = FINALSTABLE1; /*** THIS CREATES YOUR SAS DATA SET RESULTS FROM THE PROC SURVEYFREQ RESULTS ***/
PROC SURVEYFREQ DATA = PROJECT;
    STRATA  STRATUM;  /*** STRATUM VARIABLE FOR VARIANCE ESTIMATION ***/
    CLUSTER SEQNUMT;  /*** UNIQUE TEEN IDENTIFIER ***/
    WEIGHT  PROVWT_C; /*** FINAL DUAL-FRAME PROVIDER-PHASE WEIGHT (EXCLUDES TERRITORIES) ***/

    TABLES  PDAT2*(P_UTDHPV NEWAGE SEX MYRACEETH EDUC1 MARITAL2 AGEGRP_M_I MYINCTOPOV INS_STAT2_I MYCKUP1112 CEN_REG MYFACILITY)/ CL (TYPE=CP) NOWT NOSTD;
RUN;

/*** THIS PROCEDURE DISPLAYS ALL THE VARIABLE NAMES IN THE DATA SET IN ALPHABETICAL ORDER ***/
PROC CONTENTS DATA = FINALSTABLE1 SHORT;
RUN;

DATA FINALSTABLE1V1;
	LENGTH FNLESTIMATE MYRESULTS $20.;
	SET FINALSTABLE1(KEEP = AGEGRP_M_I CEN_REG EDUC1 FREQUENCY INS_STAT2_I LOWERCL MARITAL2 MYCKUP1112 MYFACILITY MYINCTOPOV 
					 MYRACEETH NEWAGE PDAT2 P_UTDHPV PERCENT SEX TABLE UPPERCL _SKIPLINE);

	IF  _SKIPLINE IN (1) THEN DELETE;

	MYESTMATE = PUT(ROUND(PERCENT,0.1), 5.1);
	CONINTEL = '('||PUT(ROUND(LOWERCL,0.1), 5.1)||' - '||PUT(ROUND(UPPERCL,0.1), 5.1)||')';
	CONINTELFL = COMPRESS(CONINTEL);
	FNLESTIMATE = CATX(" ",MYESTMATE," ",CONINTELFL);
	MYCIDIFF = UPPERCL - LOWERCL;
	IF (MYCIDIFF > 20) THEN MYRESULTS = FNLESTIMATE||"**";
	ELSE MYRESULTS = FNLESTIMATE;
RUN;

/*** THIS CREATES YOUR SAS DATA SET RESULTS YOU WILL NEED ***/
DATA FINALSTABLE1V2;
	RETAIN TABLE MYVARIABLE MYVARCOUNT MYVARNUM FREQUENCY FNLESTIMATE MYRESULTS;
	SET FINALSTABLE1V1;

	/*** MYVARIABLE = THE LABEL FOR EACH VALUE OF VARIABLE; MYVARCOUNT = THE VARIABLE NUMBER IN THE TABLE; MYVARNUM = THE VALUE OF EACH VARIABLE ***/
	IF P_UTDHPV NE . THEN DO;
		MYVARIABLE = PUT(P_UTDHPV, FMTUTD.);
		MYVARCOUNT = 0;
		MYVARNUM = P_UTDHPV;
	END;
    IF NEWAGE NE . THEN DO;
		MYVARIABLE = PUT(NEWAGE, MYAGE.);
		MYVARCOUNT = 1;
		MYVARNUM = NEWAGE;
	END;
	ELSE IF TEENAGEGP NE . THEN DO;
		MYVARIABLE = PUT(TEENAGEGP, FMTAGRP.);
		MYVARCOUNT = 2;
		MYVARNUM = TEENAGEGP;
	END;
	ELSE IF SEX NE . THEN DO;
		MYVARIABLE = PUT(SEX, FMTSEX.);
		MYVARCOUNT = 3;
		MYVARNUM = SEX; 
	END;
	ELSE IF MYRACEETH NE . THEN DO;
		MYVARIABLE = PUT(MYRACEETH, FMTRACE.);
		MYVARCOUNT = 4;
		MYVARNUM = MYRACEETH;
	END;
	ELSE IF EDUC1 NE . THEN DO;
		MYVARIABLE = PUT(EDUC1, FMTMEDU.);
		MYVARCOUNT = 5;
		MYVARNUM = EDUC1;
	END;
	ELSE IF MARITAL2 NE . THEN DO;
		MYVARIABLE = PUT(MARITAL2, FMTMARRY.);
		MYVARCOUNT = 6;
		MYVARNUM = MARITAL2;
	END;
	ELSE IF AGEGRP_M_I NE . THEN DO;
		MYVARIABLE = PUT(AGEGRP_M_I, FMTMAGRP.);
		MYVARCOUNT = 7;
		MYVARNUM = AGEGRP_M_I;
	END;
	ELSE IF MYINCTOPOV NE . THEN DO;
		MYVARIABLE = PUT(MYINCTOPOV, FMTINCPR.);
		MYVARCOUNT = 8;
		MYVARNUM = MYINCTOPOV;
	END;
	ELSE IF INS_STAT2_I NE . THEN DO;
		MYVARIABLE = PUT(INS_STAT2_I, FMTINS.);
		MYVARCOUNT = 9;
		MYVARNUM = INS_STAT2_I;
	END;
	ELSE IF MYCKUP1112 NE . THEN DO;
		MYVARIABLE = PUT(MYCKUP1112, FMTCKUP.);
		MYVARCOUNT = 10;
		MYVARNUM = MYCKUP1112;
	END;
	ELSE IF CEN_REG NE . THEN DO;
		MYVARIABLE = PUT(CEN_REG, FMTCENREG.);
		MYVARCOUNT = 11;
		MYVARNUM = CEN_REG;
	END;
	ELSE IF MYFACILITY NE . THEN DO;
		MYVARIABLE = PUT(MYFACILITY, FMTFACTY.);
		MYVARCOUNT = 12;
		MYVARNUM = MYFACILITY;
	END;
	
	KEEP TABLE MYVARIABLE MYVARCOUNT MYVARNUM FREQUENCY FNLESTIMATE MYRESULTS;
RUN;

PROC EXPORT DATA = FINALSTABLE1V2 OUTFILE ="&MYPATH.\TABLE4\SURVEYTABLE4.xlsx" DBMS=xlsx REPLACE;
RUN;

/**************************************************************************************************************************************************/
/**** UP-TO-DATE FLAG (PROV INFO): 2+ VARICELLA-CONTAINING SHOTS AT 12+ MONTHS OF AGE, EXCLUDING ANY VACCINATIONS AFTER THE RDD INTERVIEW DATE. ***/
/**************************************************************************************************************************************************/

/***********************************************************************/
/****             UNADJUSTED   MODEL 1                              ****/
/***********************************************************************/

%MACRO MYESTIMATES(VAR1, VAR2, VAR3, VAR4, DESCRIPTION);

/*** CREATE A DATA SET FOR VARIABLE YOU WANT TO ESTIMATE VACCINATION COVERAGE ***/
DATA FLPRJTDATA&VAR2.;
	SET PROJECT(KEEP = SEQNUMT STRATUM PROVWT_C &VAR1. &VAR2.);
RUN;

/*** SORT DATA BY VARIABLES IN THE BY STATEMENT YOU INTEND TO ESTIMATE ***/
PROC SORT DATA = FLPRJTDATA&VAR2.; 
	BY &VAR2.;
RUN;

/*** ODS OUTPUT TO CREATE A SAS DATA SET FOR ESTIMATES NEEDED ***/
ODS OUTPUT STATISTICS=SAS_EST&VAR2.; 

/*** USING SAS SURVEYMEANS TO ESTIMATE VACCINATION COVERAGE ***/
PROC SURVEYMEANS DATA = FLPRJTDATA&VAR2. NOBS SUM MEAN STDERR CLM; 
	STRATUM STRATUM; /*** STRATUM VARIABLE FOR VARIANCE ESTIMATION ***/
	CLUSTER SEQNUMT; /*** UNIQUE TEEN IDENTIFIER ***/
	WEIGHT PROVWT_C; /*** FINAL DUAL-FRAME PROVIDER-PHASE WEIGHT (EXCLUDES TERRITORIES) ***/
	CLASS &VAR1.; 
	VAR &VAR1.; 
	BY &VAR2.; 
RUN;

/*** THIS PROCEDURE DISPLAYS ALL THE VARIABLE NAMES IN THE DATA SET IN ALPHABETICAL ORDER ***/
PROC CONTENTS DATA = SAS_EST&VAR2. SHORT;
RUN;

DATA SAS_ESTV1&VAR2.;
	SET SAS_EST&VAR2.; 
	MEAN = MEAN*100; *CONVERT TO PERCENT ESTIMATES; 
	STDERR = STDERR*100; *CONVERT TO PERCENT ESTIMATES;
	LOWERCLMEAN = LOWERCLMEAN*100;*CONVERT TO PERCENT ESTIMATES;
	UPPERCLMEAN = UPPERCLMEAN*100;*CONVERT TO PERCENT ESTIMATES;

	MYESTMATE = PUT(ROUND(MEAN,0.1), 5.1);
	CONINTEL = '('||PUT(ROUND(LOWERCLMEAN,0.1), 5.1)||' - '||PUT(ROUND(UPPERCLMEAN,0.1), 5.1)||')';
	CONINTELFL = COMPRESS(CONINTEL);
	FNLESTIMATE = COMPRESS(CATX(" ",MYESTMATE," ",CONINTELFL));
	MYCIDIFF = UPPERCL - LOWERCL;
	IF (MYCIDIFF > 20) THEN MYRESULTS = COMPRESS(FNLESTIMATE||"**");
	ELSE MYRESULTS = COMPRESS(FNLESTIMATE);
RUN; 

DATA SAS_ESTV2&VAR2.;
	RETAIN VARNAME VARLEVEL MYVARCOUNT MYVARNUM MYVARIABLE MEAN STDERR LOWERCLMEAN UPPERCLMEAN MYRESULTS;
	LENGTH MYVARIABLE $30.0 ;
	SET SAS_ESTV1&VAR2.;
	WHERE VARLEVEL IN ("1");

	MYVARCOUNT = &VAR4.;
	MYVARNUM = &VAR2.;
	MYVARIABLE = PUT(&VAR2.,&VAR3.);
	
	

	KEEP VARNAME VARLEVEL MYVARCOUNT MYVARNUM MYVARIABLE MEAN STDERR LOWERCLMEAN UPPERCLMEAN MYRESULTS;
RUN;

PROC PRINT DATA=SAS_ESTV2&VAR2. NOOBS LABEL; 
	FORMAT MYVARNUM &VAR3.; 
	FORMAT MEAN STDERR 5.2; 
	VAR MYVARNUM MEAN STDERR MYRESULTS; 
	LABEL 
	MEAN='PERCENT UP-TO-DATE' 
	STDERR='STANDARD ERROR'
	MYRESULTS = "WEIGHTED PERCENT AND 95% C.L.";
	TITLE "&VAR1. ESTIMATES BY ESTIMATION DEMOGRAPHIC VARIABLE"; 
RUN;

PROC APPEND BASE = FINALTABLE2 DATA=SAS_ESTV2&VAR2. FORCE;
RUN;

%MEND MYESTIMATES;

%MYESTIMATES(P_UTDHPV, NEWAGE, MYAGE., 1, "AGE IN YEARS OF SELECTED TEEN");
%MYESTIMATES(P_UTDHPV, SEX, FMTSEX., 2, "SEX OF TEEN");
%MYESTIMATES(P_UTDHPV, MYRACEETH, FMTRACE., 3, "RACE/ETHNICITY OF TEEN WITH MULTIRACE CATEGORY (RECODE)");
%MYESTIMATES(P_UTDHPV, EDUC1, FMTMEDU., 4, "EDUCATION LEVEL OF MOTHER WITH 4 CATEGORIES (RECODE)");
%MYESTIMATES(P_UTDHPV, MARITAL2, FMTMARRY., 5, "MARITAL STATUS OF MOTHER (RECODE)");
%MYESTIMATES(P_UTDHPV, AGEGRP_M_I, FMTMAGRP., 6, "MOTHER'S AGE CATEGORIES (RECODE)");
%MYESTIMATES(P_UTDHPV, MYINCTOPOV, FMTINCPR., 7, "(INCOME TO POVERTY RATIO: IMPUTED (RECODE)");
%MYESTIMATES(P_UTDHPV, INS_STAT2_I, FMTINS., 8, "INSURANCE STATUS (PRIVATE ONLY/ANY MEDICAID/OTHER INSURANCE/UNINSURED): IMPUTED");
%MYESTIMATES(P_UTDHPV, MYCKUP1112, FMTCKUP., 9, "DID TEEN HAVE AN 11-12 YEAR OLD WELL-CHILD EXAM OR CHECK-UP?");
%MYESTIMATES(P_UTDHPV, CEN_REG, FMTCENREG., 10, "CENSUS REGION BASED ON TRUE STATE OF RESIDENCE");
%MYESTIMATES(P_UTDHPV, MYFACILITY, FMTFACTY., 11, "FACILITY TYPES FOR TEEN'S PROVIDERS");


DATA PUF.FINALTABLE2;
	SET FINALTABLE2;
RUN;
PROC EXPORT DATA = FINALTABLE2 OUTFILE= "&MYPATH.\TABLE5\FINALTABLE2.xlsx"
	DBMS=xlsx REPLACE;
RUN;


/***********************************************************************/
/****             UNADJUSTED   MODEL 2  ODDS RATIOS                 ****/
/***********************************************************************/

/****** MODEL 2 ODDS RATIO PROGRAM *********/
%MACRO MYODDSRATIO(VAR1, VAR2, VAR3, VAR4, DESCRIPTION);
PROC SURVEYLOGISTIC DATA = PROJECT;
    STRATA  STRATUM; /*** STRATUM VARIABLE FOR VARIANCE ESTIMATION ***/
    CLUSTER SEQNUMT; /*** UNIQUE TEEN IDENTIFIER ***/
    WEIGHT PROVWT_C; /*** FINAL DUAL-FRAME PROVIDER-PHASE WEIGHT (EXCLUDES TERRITORIES) ***/
	DOMAIN PDAT2;    /*** ADEQUATE PROVIDER DATA FLAG ***/

	CLASS &VAR3.;

    MODEL  P_UTDHPV(EVENT = "1") = &VAR1.;
	
	ODS OUTPUT PARAMETERESTIMATES=MYUNADJPARAEST&VAR1.;
	ODS OUTPUT ODDSRATIOS=MYOREST&VAR1.;
	ODS OUTPUT MODELANOVA=MYTYPE3TEST&VAR1.;
RUN;

/*** CREATING RESULTS FOR ODDS RATIOS ***/

DATA MYORESTV1&VAR1.;
	RETAIN NEWEFFECT MYRESULTS MYESTMATE CONINTELFL;
	LENGTH MYRESULTS NEWEFFECT $20.;
	SET MYOREST&VAR1.;
	WHERE PDAT2 = 1;

	NEWEFFECT = EFFECT;
	
	MYESTMATE = PUT(ROUND(ODDSRATIOEST,0.01), 5.2);
	CONINTEL = '('||PUT(ROUND(LOWERCL,0.01), 5.2)||' - '||PUT(ROUND(UPPERCL,0.01), 5.2)||')';
	CONINTELFL = COMPRESS(CONINTEL);
	MYRESULTS = CATX(" ",MYESTMATE," ",CONINTELFL);
	
RUN;

DATA MYORESTV2&VAR1.;
	RETAIN MYVARCOUNT NEWEFFECT MYRESULTS MYESTMATE CONINTELFL;
	LENGTH MYVARIABLE NEWEFFECT $20.;
	SET MYORESTV1&VAR1.;

	MYVARCOUNT = &VAR4.;
	
	KEEP MYVARCOUNT NEWEFFECT MYRESULTS MYESTMATE CONINTELFL;
RUN;

PROC APPEND BASE = ODDSRATIOSTABLE3 DATA=MYORESTV2&VAR1. FORCE;
RUN;

/*** CREATING RESULTS FOR SIGNIFICANCE T-TESTS AND P-VALUES WITHIN MODEL VARIABLES  ***/

PROC CONTENTS DATA = MYUNADJPARAEST&VAR1. SHORT;
RUN;

DATA MYUNADJPARAESTV1&VAR1.;
	RETAIN MYVARNUM VARIABLE EFFECT MYVARIABLE MYTVALUE MYPROBT;
	LENGTH MYVARIABLE $40.;
	SET MYUNADJPARAEST&VAR1.(KEEP = PDAT2 VARIABLE CLASSVAL0 TVALUE PROBT);
	WHERE PDAT2 = 1;

	IF VARIABLE IN ("Intercept") THEN DELETE;

	EFFECT = INPUT(CLASSVAL0, 8.);

	IF VARIABLE IN ("&VAR1.") THEN DO;
		MYVARIABLE = PUT(EFFECT, &VAR2.);
		MYVARNUM = &VAR4.;
	END;

	MYTVALUE = PUT(ROUND(TVALUE, 0.001), 8.3);
	MYPROBT = PUT(ROUND(PROBT, 0.01), 8.2);
		
	KEEP MYVARNUM VARIABLE EFFECT MYVARIABLE MYTVALUE MYPROBT;
RUN;

PROC APPEND BASE = MYUNADJPARATTEST DATA=MYUNADJPARAESTV1&VAR1. FORCE;
RUN;


/*** CREATING RESULTS FOR TYPE III TEST OF EFFECT FOR MODELS  ***/

DATA MYTYPE3TESTV1&VAR1.;
	RETAIN MYVARCOUNT NEWEFFECT MYVARIABLE MYWALDCHISQ MYPROBCHISQ;
	LENGTH MYVARIABLE NEWEFFECT $20.;
	SET MYTYPE3TEST&VAR1.;
	WHERE PDAT2 = 1;

	MYVARCOUNT = &VAR4.;

	NEWEFFECT = EFFECT;

	MYWALDCHISQ = PUT(ROUND(WALDCHISQ, 0.001), 8.3);
	MYPROBCHISQ = PUT(ROUND(PROBCHISQ, 0.01), 8.2);
	
	KEEP MYVARCOUNT NEWEFFECT MYWALDCHISQ MYPROBCHISQ;
RUN;

PROC APPEND BASE = MYTYPE3TEST DATA=MYTYPE3TESTV1&VAR1. FORCE;
RUN;

%MEND MYODDSRATIO;

%MYODDSRATIO(NEWAGE, MYAGE., NEWAGE(PARAM=REF REF="5"), 1, "AGE IN YEARS OF SELECTED TEEN");
%MYODDSRATIO(SEX, FMTSEX., SEX(PARAM=REF REF="2"), 2, "SEX OF TEEN");
%MYODDSRATIO(MYRACEETH, FMTRACE., MYRACEETH(PARAM=REF REF="1"), 3, "RACE/ETHNICITY OF TEEN WITH MULTIRACE CATEGORY (RECODE)");
%MYODDSRATIO(EDUC1, FMTMEDU., EDUC1(PARAM=REF REF="1"), 4, "EDUCATION LEVEL OF MOTHER WITH 4 CATEGORIES (RECODE)");
%MYODDSRATIO(MARITAL2, FMTMARRY., MARITAL2(PARAM=REF REF="1"), 5, "MARITAL STATUS OF MOTHER (RECODE)");
%MYODDSRATIO(AGEGRP_M_I, FMTMAGRP., AGEGRP_M_I(PARAM=REF REF="1"), 6, "MOTHER'S AGE CATEGORIES (RECODE)");
%MYODDSRATIO(MYINCTOPOV, FMTINCPR., MYINCTOPOV(PARAM=REF REF="1"), 7, "(INCOME TO POVERTY RATIO: IMPUTED (RECODE)");
%MYODDSRATIO(INS_STAT2_I, FMTINS., INS_STAT2_I(PARAM=REF REF="1"), 8, "INSURANCE STATUS (PRIVATE ONLY/ANY MEDICAID/OTHER INSURANCE/UNINSURED): IMPUTED");
%MYODDSRATIO(MYCKUP1112, FMTCKUP., MYCKUP1112(PARAM=REF REF="1"), 9, "DID TEEN HAVE AN 11-12 YEAR OLD WELL-CHILD EXAM OR CHECK-UP?");
%MYODDSRATIO(CEN_REG, FMTCENREG., CEN_REG(PARAM=REF REF="1"), 10, "CENSUS REGION BASED ON TRUE STATE OF RESIDENCE");
%MYODDSRATIO(MYFACILITY, FMTFACTY., MYFACILITY(PARAM=REF REF="1"), 11, "FACILITY TYPES FOR TEEN'S PROVIDERS");

DATA PUF.ODDSRATIOSTABLE3;
	SET ODDSRATIOSTABLE3;
RUN;

PROC EXPORT DATA = ODDSRATIOSTABLE3 OUTFILE ="&MYPATH.\TABLE5\ODDSRATIOSTABLE3.xlsx" DBMS=xlsx REPLACE;
RUN;

DATA PUF.MYUNADJPARATTEST;
	SET MYUNADJPARATTEST;
RUN;

PROC EXPORT DATA = MYUNADJPARATTEST OUTFILE ="&MYPATH.\TABLE5\MYUNADJPARATTESTTABLE3.xlsx" DBMS=xlsx REPLACE;
RUN;

DATA PUF.MYTYPE3TEST;
	SET MYTYPE3TEST;
RUN;

PROC EXPORT DATA = MYTYPE3TEST OUTFILE ="&MYPATH.\TABLE6\MYTYPE3TESTTABLE3.xlsx" DBMS=xlsx REPLACE;
RUN;



/****************************************************************************************************/
/*** THIS PROGRAM PRODUCES THE DISTRIBUTION OF WEIGHTED FREQUENCIES FOR EACH VARIABLE OF INTEREST ***/
/***      AND THE CHI-SQUARE STATISTICS AND P-VALUE FOR EACH VARIABLE OF INTEREST.                ***/
/****************************************************************************************************/

/***********************************************************************/
/****                 ADJUSTED MODEL 3  ODDS RATIOS                 ****/
/***********************************************************************/

TITLE1 "FITTING A LOGISTIC REGRESSION MODEL ACCOUNTING FOR COMPLEX SURVEY DESIGN FEATURES";
TITLE2 'USING NIS-TEEN PUBLIC USER FILES 2018 AND UNGROUPED AGES (13 TO 17 YEARS) AT INTERVIEW';

PROC SURVEYLOGISTIC DATA = PROJECT;
    STRATA  STRATUM; /*** STRATUM VARIABLE FOR VARIANCE ESTIMATION ***/
    CLUSTER SEQNUMT; /*** UNIQUE TEEN IDENTIFIER ***/
    WEIGHT PROVWT_C; /*** FINAL DUAL-FRAME PROVIDER-PHASE WEIGHT (EXCLUDES TERRITORIES) ***/
	DOMAIN PDAT2;    /*** ADEQUATE PROVIDER DATA FLAG ***/

	CLASS NEWAGE(PARAM=REF REF="5") SEX(PARAM=REF REF="2") MYRACEETH(PARAM=REF REF="1") EDUC1(PARAM=REF REF="1") MARITAL2(PARAM=REF REF="1") AGEGRP_M_I(PARAM=REF REF="1") 
          MYINCTOPOV(PARAM=REF REF="1") INS_STAT2_I(PARAM=REF REF="1") MYCKUP1112(PARAM=REF REF="1") CEN_REG(PARAM=REF REF="1") MYFACILITY(PARAM=REF REF="1");

    MODEL  P_UTDHPV(EVENT = "1") = NEWAGE SEX MYRACEETH EDUC1 MARITAL2 AGEGRP_M_I MYINCTOPOV INS_STAT2_I MYCKUP1112 CEN_REG MYFACILITY;

	ODS OUTPUT PARAMETERESTIMATES=MYADJPARAEST;
	ODS OUTPUT ODDSRATIOS=MYADJOREST;
	ODS OUTPUT MODELANOVA=MYADJTYPE3TEST;
RUN;

/*** CREATING RESULTS FOR ODDS RATIOS ***/

DATA MYADJORESTV1;
	RETAIN NEWEFFECT MYRESULTS MYESTMATE CONINTELFL;
	LENGTH MYRESULTS NEWEFFECT $20.;
	SET MYADJOREST;
	WHERE PDAT2 = 1;

	NEWEFFECT = EFFECT;
	
	MYESTMATE = PUT(ROUND(ODDSRATIOEST,0.01), 5.2);
	CONINTEL = '('||PUT(ROUND(LOWERCL,0.01), 5.2)||' - '||PUT(ROUND(UPPERCL,0.01), 5.2)||')';
	CONINTELFL = COMPRESS(CONINTEL);
	MYRESULTS = CATX(" ",MYESTMATE," ",CONINTELFL);
	
RUN;

DATA MYADJORESTV2;
	RETAIN NEWEFFECT MYRESULTS MYESTMATE CONINTELFL;
	SET MYADJORESTV1;

	KEEP NEWEFFECT MYRESULTS MYESTMATE CONINTELFL;
RUN;

PROC EXPORT DATA = MYADJORESTV2 OUTFILE ="&MYPATH.\TABLE5\MULTILOGODDSRATIOTABLE3.xlsx" DBMS=xlsx REPLACE;
RUN;

/*** CREATING RESULTS FOR SIGNIFICANCE T-TESTS AND P-VALUES WITHIN MODEL VARIABLES  ***/

PROC CONTENTS DATA = MYADJPARAEST SHORT;
RUN;

DATA MYADJPARAESTV1;
	RETAIN MYVARNUM VARIABLE EFFECT MYVARIABLE MYTVALUE MYPROBT;
	LENGTH MYVARIABLE $40.;
	SET MYADJPARAEST(KEEP = PDAT2 VARIABLE CLASSVAL0 TVALUE PROBT);
	WHERE PDAT2 = 1;

	IF VARIABLE IN ("Intercept") THEN DELETE;

	EFFECT = INPUT(CLASSVAL0, 8.);

	IF VARIABLE IN ("NEWAGE") THEN DO;
		MYVARIABLE = PUT(EFFECT, MYAGE.);
		MYVARNUM = 1;
	END;
	ELSE IF VARIABLE IN ("SEX") THEN DO;
		MYVARIABLE = PUT(EFFECT, FMTSEX.);
		MYVARNUM = 2;
	END;
	ELSE IF VARIABLE IN ("MYRACEETH") THEN DO;
		MYVARIABLE = PUT(EFFECT, FMTRACE.);
		MYVARNUM = 3;
	END;
	ELSE IF VARIABLE IN ("EDUC1") THEN DO;
		MYVARIABLE = PUT(EFFECT, FMTMEDU.);
		MYVARNUM = 4;
	END;
	ELSE IF VARIABLE IN ("MARITAL2") THEN DO;
		MYVARIABLE = PUT(EFFECT, FMTMARRY.);
		MYVARNUM = 5;
	END;
	ELSE IF VARIABLE IN ("AGEGRP_M_I") THEN DO;
		MYVARIABLE = PUT(EFFECT, FMTMAGRP.);
		MYVARNUM = 6;
	END;
	ELSE IF VARIABLE IN ("MYINCTOPOV") THEN DO;
		MYVARIABLE = PUT(EFFECT, FMTINCPR.);
		MYVARNUM = 7;
	END;
	ELSE IF VARIABLE IN ("INS_STAT2_I") THEN DO;
		MYVARIABLE = PUT(EFFECT, FMTINS.);
		MYVARNUM = 8;
	END;
	ELSE IF VARIABLE IN ("MYCKUP1112") THEN DO;
		MYVARIABLE = PUT(EFFECT, FMTCKUP.);
		MYVARNUM = 9;
	END;
	ELSE IF VARIABLE IN ("CEN_REG") THEN DO;
		MYVARIABLE = PUT(EFFECT, FMTCENREG.);
		MYVARNUM = 10;
	END;
	ELSE IF VARIABLE IN ("MYFACILITY") THEN DO;
		MYVARIABLE = PUT(EFFECT, FMTFACTY.);
		MYVARNUM = 11;
	END;

	MYTVALUE = PUT(ROUND(TVALUE, 0.001), 8.3);
	MYPROBT = PUT(ROUND(PROBT, 0.01), 8.2);
	
	KEEP MYVARNUM VARIABLE EFFECT MYVARIABLE MYTVALUE MYPROBT;
RUN;

PROC EXPORT DATA = MYADJPARAESTV1 OUTFILE ="&MYPATH.\TABLE5\MULTILOGTTESTTABLE3.xlsx" DBMS=xlsx REPLACE;
RUN;

/*** CREATING RESULTS FOR TYPE III TEST OF EFFECT FOR MODELS  ***/

DATA MYADJTYPE3TESTV1;
	RETAIN NEWEFFECT MYWALDCHISQ MYPROBCHISQ;
	LENGTH NEWEFFECT $20.;
	SET MYADJTYPE3TEST;
	WHERE PDAT2 = 1;

	NEWEFFECT = EFFECT;

	MYWALDCHISQ = PUT(ROUND(WALDCHISQ, 0.001), 8.3);
	MYPROBCHISQ = PUT(ROUND(PROBCHISQ, 0.01), 8.2);
	
	KEEP NEWEFFECT MYWALDCHISQ MYPROBCHISQ;
RUN;

PROC EXPORT DATA = MYADJTYPE3TESTV1 OUTFILE ="&MYPATH.\TABLE6\MULTILOGTYPE3TESTTABLE3.xlsx" DBMS=xlsx REPLACE;
RUN;

/***********************************************************************/
/****                 ADJUSTED MODEL 4  ODDS RATIOS                 ****/
/***********************************************************************/

TITLE1 "FITTING A LOGISTIC REGRESSION MODEL ACCOUNTING FOR COMPLEX SURVEY DESIGN FEATURES";
TITLE2 'USING NIS-TEEN PUBLIC USER FILES 2018 AND UNGROUPED AGES (13 TO 17 YEARS) AT INTERVIEW';

PROC SURVEYLOGISTIC DATA = PROJECT;
    STRATA  STRATUM; /*** STRATUM VARIABLE FOR VARIANCE ESTIMATION ***/
    CLUSTER SEQNUMT; /*** UNIQUE TEEN IDENTIFIER ***/
    WEIGHT PROVWT_C; /*** FINAL DUAL-FRAME PROVIDER-PHASE WEIGHT (EXCLUDES TERRITORIES) ***/
	DOMAIN PDAT2;    /*** ADEQUATE PROVIDER DATA FLAG ***/

	CLASS SEX(PARAM=REF REF="2") MYRACEETH(PARAM=REF REF="1") EDUC1(PARAM=REF REF="1")
          MYINCTOPOV(PARAM=REF REF="1") INS_STAT2_I(PARAM=REF REF="1") MYCKUP1112(PARAM=REF REF="1") CEN_REG(PARAM=REF REF="1") MYFACILITY(PARAM=REF REF="1");

    MODEL  P_UTDHPV(EVENT = "1") = SEX MYRACEETH EDUC1 MYINCTOPOV INS_STAT2_I MYCKUP1112 CEN_REG MYFACILITY;

	ODS OUTPUT PARAMETERESTIMATES=MYPARAESTML4;
	ODS OUTPUT ODDSRATIOS=MYORESTML4;
	ODS OUTPUT MODELANOVA=MYTYPE3TESTML4;
RUN;

/*** CREATING RESULTS FOR ODDS RATIOS ***/

DATA MYORESTML4V1;
	RETAIN NEWEFFECT MYRESULTS MYESTMATE CONINTELFL;
	LENGTH MYRESULTS NEWEFFECT $20.;
	SET MYORESTML4;
	WHERE PDAT2 = 1;

	NEWEFFECT = EFFECT;
	
	MYESTMATE = PUT(ROUND(ODDSRATIOEST,0.01), 5.2);
	CONINTEL = '('||PUT(ROUND(LOWERCL,0.01), 5.2)||' - '||PUT(ROUND(UPPERCL,0.01), 5.2)||')';
	CONINTELFL = COMPRESS(CONINTEL);
	MYRESULTS = CATX(" ",MYESTMATE," ",CONINTELFL);
	
RUN;

DATA MYORESTML4V2;
	RETAIN NEWEFFECT MYRESULTS MYESTMATE CONINTELFL;
	SET MYORESTML4V1;

	KEEP NEWEFFECT MYRESULTS MYESTMATE CONINTELFL;
RUN;

PROC EXPORT DATA = MYORESTML4V2 OUTFILE ="&MYPATH.\TABLE5\MULTILOGODDSRATIOTABLE3ML4.xlsx" DBMS=xlsx REPLACE;
RUN;

/*** CREATING RESULTS FOR SIGNIFICANCE T-TESTS AND P-VALUES WITHIN MODEL VARIABLES  ***/

PROC CONTENTS DATA = MYADJPARAEST SHORT;
RUN;

DATA MYADJPARAESTML4V1;
	RETAIN MYVARNUM VARIABLE EFFECT MYVARIABLE MYTVALUE MYPROBT;
	LENGTH MYVARIABLE $40.;
	SET MYADJPARAESTML4(KEEP = PDAT2 VARIABLE CLASSVAL0 TVALUE PROBT);
	WHERE PDAT2 = 1;

	IF VARIABLE IN ("Intercept") THEN DELETE;

	EFFECT = INPUT(CLASSVAL0, 8.);

	IF VARIABLE IN ("NEWAGE") THEN DO;
		MYVARIABLE = PUT(EFFECT, MYAGE.);
		MYVARNUM = 1;
	END;
	ELSE IF VARIABLE IN ("EDUC1") THEN DO;
		MYVARIABLE = PUT(EFFECT, FMTMEDU.);
		MYVARNUM = 2;
	END;
	ELSE IF VARIABLE IN ("MYCKUP1112") THEN DO;
		MYVARIABLE = PUT(EFFECT, FMTCKUP.);
		MYVARNUM = 3;
	END;
	ELSE IF VARIABLE IN ("CEN_REG") THEN DO;
		MYVARIABLE = PUT(EFFECT, FMTCENREG.);
		MYVARNUM = 4;
	END;
	ELSE IF VARIABLE IN ("MYFACILITY") THEN DO;
		MYVARIABLE = PUT(EFFECT, FMTFACTY.);
		MYVARNUM = 5;
	END;

	MYTVALUE = PUT(ROUND(TVALUE, 0.001), 8.3);
	MYPROBT = PUT(ROUND(PROBT, 0.01), 8.2);
	
	KEEP MYVARNUM VARIABLE EFFECT MYVARIABLE MYTVALUE MYPROBT;
RUN;

PROC EXPORT DATA = MYADJPARAESTML4V1 OUTFILE ="&MYPATH.\TABLE5\MULTILOGTTESTTABLE3ML4.xlsx" DBMS=xlsx REPLACE;
RUN;

/*** CREATING RESULTS FOR TYPE III TEST OF EFFECT FOR MODELS  ***/

DATA MYTYPE3TESTML4V1;
	RETAIN NEWEFFECT MYWALDCHISQ MYPROBCHISQ;
	LENGTH NEWEFFECT $20.;
	SET MYTYPE3TESTML4;
	WHERE PDAT2 = 1;

	NEWEFFECT = EFFECT;

	MYWALDCHISQ = PUT(ROUND(WALDCHISQ, 0.001), 8.3);
	MYPROBCHISQ = PUT(ROUND(PROBCHISQ, 0.01), 8.2);
	
	KEEP NEWEFFECT MYWALDCHISQ MYPROBCHISQ;
RUN;

PROC EXPORT DATA = MYTYPE3TESTML4V1 OUTFILE ="&MYPATH.\TABLE6\MULTILOGTYPE3TESTTABLE3ML4.xlsx" DBMS=xlsx REPLACE;
RUN;













